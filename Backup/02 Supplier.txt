;;; Shivgoraksh

[Variable: SupplierLV]
	
	Type: String

;[#Menu: Gateway of Tally]
;	
;	Add: Item: ERPNextSupplier  : Call: ERPNextSupplier
	
	[Function: ERPNextSupplier]
		
		List Variable: SupplierLV

		12 : Delete File: "STSupplier.xml"
		13 : Open File: "STSupplier.xml" : Text : Write
		14 : Truncate File

		16 : Walk Collection: ERPNextSupplierColl
		18 : Write File Line :     '    <TALLYMESSAGE xmlns:UDF="TallyUDF">'
		20 : Write File Line :     '     <LEDGER NAME="'+$supplier_name+'" RESERVEDNAME="">'
		
		22 : Write File Line :     '      <ADDRESS.LIST TYPE="String">'
		23 : Walk Collection: taddress
		24 : 	Do If: Not ($$IsEmpty:$address_line1) 	: Write File Line :     '       <ADDRESS>'+$address_line1+'</ADDRESS>'
		26 : 	Do If: Not ($$IsEmpty:$address_line2) 	: Write File Line :     '       <ADDRESS>'+$address_line2+'</ADDRESS>'
		28 : 	Do If: Not ($$IsEmpty:$city) 			: Write File Line :     '       <ADDRESS>'+$city+'</ADDRESS>'
		28a: 	Do If: Not ($$IsEmpty:$state) 			: Write File Line :     '       <ADDRESS>'+$state+'</ADDRESS>'
		30 : 	Write File Line :     '      <PARTYGSTIN>'+$gstin+'</PARTYGSTIN>'    
		31 : 	Write File Line :     '      <LEDSTATENAME>'+$state+'</LEDSTATENAME>'    
		32 : 	Write File Line :     '      <COUNTRYNAME>'+$country+'</COUNTRYNAME>'    
		33 : End Walk 
		34 : Write File Line :     '      </ADDRESS.LIST>'

;		39 : Write File Line :     '      <EMAIL>'+$email+'</EMAIL>'
		40 : Write File Line :     '      <GSTREGISTRATIONTYPE>'+@@gst_category+'</GSTREGISTRATIONTYPE>'
		50 : Write File Line :     '      <PARENT>Sundry Creditors</PARENT>'
;		60 : Write File Line :     '      <LEDGERMOBILE>'+$email_id+'</LEDGERMOBILE>'
		70 : Write File Line :     '      <ISBILLWISEON>Yes</ISBILLWISEON>'
		80 : Write File Line :     '      <ASORIGINAL>Yes</ASORIGINAL>'
		
		
		90 : Write File Line :     '      <LANGUAGENAME.LIST>'
		100: Write File Line :     '       <NAME.LIST TYPE="String">'
		110: Write File Line :     '        <NAME>'+$supplier_name+'</NAME>'
		120: Write File Line :     '       </NAME.LIST>'
		130: Write File Line :     '      </LANGUAGENAME.LIST>'
		140: Write File Line :     '     </LEDGER>'
		150: Write File Line :     '    </TALLYMESSAGE>'
		152 : List Add: SupplierLV : $name : $name
		155 : End Walk

		200 : Close Target File
		210 : SET : IMPORT File : "STSupplier.xml" 	
		220 : Import : All Masters : Yes

		230 : If : Not $$ImportInfo:Errors > 0		
		240 : 	Call : UpdateSupplierStatus:($$NumItems:SupplierLVColl)
		242 : else
		245 : Msg Box: "Error" : "Something Went Wrong, Please contact support"
		250 : End If

		
[Collection: ERPNextSupplierColl]
	
	Export Header	: "Accept: application/json"
	Export Header	: "Accept-Charset: utf-8"
	Export Header	: "Authorization: " + "token " + @@AuthToken

	Data Source: HTTP JSON : @@ERPNextHost + '/api/method/express_tally.send_tally.supplier'
	RemoteRequest: SupplierReq : UTF8
	JSON Object : ERPNextSupplier
	JSON Object Path: "message:1"
	

	[Object: ERPNextSupplier]
		
		Storage : name 			: String
		Storage : supplier_Name : String
		Storage : gst_category 	: String
		Storage : email_id	 	: String
		Storage	: mobile_no	 	: String
		Storage : pan	 		: String
	
		collection : taddress : taddress
		
;		[Object: taddress]
;			
;			Storage: address_line1	 : String
;			Storage: city	 : String
;			Storage: county	 : String
;			Storage: state	 : String
;			Storage: gstin	 : String
;			Storage: gst_state	 : String
			

;; Request Report 

	[Report: SupplierReq]
		
		Form: SupplierReq
		
		[Form: SupplierReq]
			
			Part: SupplierReq
			Delete: XMLTag

			[Part: SupplierReq]
					
				Line: SupplierReq
				Scroll: Vertical

				[Line: SupplierReq]
					
					Field: SupplierReqBranch, SupplierReqSyncFrom
					;XMLTag: "request"
					
				[Field: SupplierReqBranch]
					
					XMLTag: "company"
					Set as: @@ERPNextBranch
					
				[Field: SupplierReqSyncFrom]
					
					XMLTag: "date"
					Set as: @@ERPNextLastSyncOn
					
;; End of Request Report



;; Master Success 

[Function: UpdateSupplierStatus]
	
	Parameter: processedIds : Number

	08 : If: ##processedIds > 0 
	10 : Walk Collection: UpdateSupplierColl
	20 : End Walk
	30 : End If
	40 : Delete File: "STSupplier.xml"




[Collection: UpdateSupplierColl]
	
	Export Header	: "Accept: application/json"
	Export Header	: "Accept-Charset: utf-8"
	Export Header	: "Authorization: " + "token " + @@AuthToken

	Data Source	 : HTTP JSON: @@ERPNextHost + '/api/method/express_tally.send_tally.customer_update'
	RemoteRequest : UpdateSupplierRep : UTF8

;; Status Report 

[Report: UpdateSupplierRep]
	
	Form: UpdateSupplierRep
	
	[Form: UpdateSupplierRep]
		
		Part: UpdateSupplierRep
		Delete: XMLTag

	[Part: UpdateSupplierRep]
		
		Line: UpdateSupplierRep
		Scroll: Vertical
		
	[Line: UpdateSupplierRep]
		
		Field: UpdateSupplierRep
		Explode: UpdateSupplierRepExp
		
			[Part: UpdateSupplierRepExp]
				
				Line: UpdateSupplierRepExp
				Repeat: UpdateSupplierRepExp : SupplierLVColl
				
				[Line: UpdateSupplierRepExp]
					
					Field: UpdateSupplierRepExp
					JSONTag: "data"
					
					[Field:UpdateSupplierRepExp]
						
						Set as: $SupplierLV
						XMLTag: "docname"
		
		[Field: UpdateSupplierRep]
			
			Set as: "Supplier" ;; erpnext doc type
			JSONTag: "request_type"
	
	[Collection: SupplierLVColl]
		
		Data Source: Variable: SupplierLV 