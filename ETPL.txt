;;Shivgoraksh


[#System: Formula]
	
;; Purchase Invoice

	PISupplier_Name : if $$IsEmpty:$supplier_name_in_tally or $supplier_name_in_tally ="null" then $supplier_name else $supplier_name_in_tally


	PIPurchaseLEd : if $tax_category = "In-State" then "Purchase Taxable GST" +
							else if $tax_category = "Out-State" then "Interstate Purchase Taxable (GST)" +
							else if $tax_category = "Without GST" then "Purchase Return Without GST" +
							else "GST Purchase"

	PIAccountHead : if $account_head contains "CGST" then "Input Tax CGST" else +
					if $account_head contains "SGST" then "Input Tax SGST" else +
					If $account_head contains "IGST" then "Input Tax IGST" else $$Trim_abbr:$account_head


;; Sales Invoice 

	PICustomer_Name : if $$IsEmpty:$supplier_name_in_tally or $supplier_name_in_tally ="null" then $customer_name else $supplier_name_in_tally


	SIAccountHead : if $account_head contains "CGST" then "Output Tax CGST" else +
					if $account_head contains "SGST" then "Output Tax SGST" else +
					If $account_head contains "IGST" then "Output Tax IGST" else $$Trim_abbr:$account_head

	SISalesLed	: if $tax_category = "In-State" then "Sales Taxable GST" +
							else if $tax_category = "Out-State" then "Interstate Sales Taxable (GST)" +
							else if $tax_category = "Without GST" then "Sales Return Without GST" +
							else "GST Purchase"
							








;;; Unwanted Struff 

[Collection: ERPNextHeaderColl]
	
	Export Header	: "Accept: application/json"
	Export Header	: "Accept-Charset: utf-8"
	Export Header	: "Authorization: " + "token " + @@AuthToken

	Data Source: HTTP JSON: @@ERPNextHost + '/api/resource/Sales%20Invoice?'++
							'fields=["posting_date","name","docstatus","customer_name","paid_amount"]&'++
							'filters=[["Sales%20Invoice","posting_date",">=","'+($$GetSqlDate:##SVFromDate)+'"],'++
							'["Sales%20Invoice","posting_date","<=","'+($$GetSqlDate:##SVTODate)+'"]]&'++
							'limit_page_length=100'
	JSON Object Path: "data:1"
	Sort: @@Default : $Name, $$GetTallyDate:$creation

;[#Menu: ExpressIntegration]
;	
;	Add:  Option: Gateway of Tally ERPNext : @@EnableERPNextImp
;	
;	[!Menu: Gateway of Tally ERPNext]
;	
;		Add: Item: Before : @@LocQuit : Get ERPNext Invoices : Menu: ERPNextMenu ; 
		
		[Menu: ERPNextMenu]
			
			Key Item: Sales Invoices : S : Display: ERPNextInvoices
;			Key Item: Payment Receipts : P : Display: ERPNextPayments
		
	
	[Report: ERPNextInvoices]
		
		Form: ERPNextInvoices
		Variable: SVFromDate, SVToDate
		Set: SVFromDate: $$MachineDate
		Set: SVToDate: $$MachineDate
		
		[Form: ERPNextInvoices]
			
			Part: ERPNextInvoices
			Button: Change Period , ERPNextImportInvoice
			
			[Part: ERPNextInvoices]
				
				Line: ERPNextInvoices
				Repeat: ERPNextInvoices: ERPNextHeaderColl
				Scroll: Vertical
				
				[Line: ERPNextInvoices]
					
					Field: ERPNextInvoicesCust,ERPNextInvoicesInTally,ERPNextInvoicesDate, ERPNextInvoicesName, ERPNextInvoicesStatus

					[Field: ERPNextInvoicesCust]
						
						Use: name Field 
						Set as: $customer_name
						Full Width: YEs

					[Field: ERPNextInvoicesInTally]
						
						Use: name Field 
						Set as: $Name:Ledger:$customer_name
						Full Width: YEs

					[Field: ERPNextInvoicesDate]
						
						Use: Uni Date Field 
						Set as: $$GetTallyDate:$posting_date
						Width: 12
						Align: Left

					[Field: ERPNextInvoicesName]
						
						Use: Name Field 
						Set as: $Name
						
					[Field: ERPNextInvoicesStatus]
						
						Use: Name Field 
						Set as: $docstatus
						


[Button: ERPNextImportInvoice]
	
	Title: "Import"
	Action: Call: ERPNextInvoiceXML
	Key: Ctrl+I
	Scope: Report

[Collection: ERPNextDetailsColl]
	
	Export Header	: "Accept: application/json"
	Export Header	: "Accept-Charset: utf-8"
	Export Header	: "Authorization: " + "token " + @@AuthToken

	Data Source: HTTP JSON: @@ERPNextHost + '/api/resource/Sales%20Invoice/'+##vName+'?'++
							'fields=["*"]'
	JSON Object Path: "data:1"

[Function: ERPNextInvoiceXML]
	
	Variable: vName : String
	Variable: vTempLog : Logical
;	List Variable : LVIdsQuote 
	Variable : ParentName : String
	Variable: vQuoteImport : Logical: Yes


	10 : Walk Collection: SelectedLines

	12 : Set: vName : $name
	15 : Delete File: "ERPNextGetInvoices"
	20 : Open File: "ERPNextGetInvoices" : Text : Write
	25 : Truncate File

	30 : 	Walk Collection : ERPNextDetailsColl
	35 : 	Call: ERPNextSalesXML
	40 : 	End Walk

	50 : Close Target File

	55 : SET : IMPORT File : "ERPNextGetInvoices" 	
	60 : Import : Vouchers : Yes
		
;	70 : If : Not $$ImportInfo:Errors > 0
;	75 : Call : RRSendStatusQuote :($$NumItems:LVCollQuote):@@GetSalesQuoteRespURL
;	80 : End If

	65 : End Walk
	
[Collection : Selected Lines]
	
	Data Source : Report  :Selected Lines
	Fetch : *.*

	
	




[Function: ERPNextSalesXML]

	Variable: vDate : String: $$GetTallyDate:$posting_date
	
08 : Write File Line : '    <TALLYMESSAGE xmlns:UDF="TallyUDF">'
10 : If: $$IsEmpty:($Name:Ledger:$customer)
11 : Write File Line : '     <LEDGER NAME="'+$$Trim:$customer+'" RESERVEDNAME="">'
12 : Write File Line : '      <ADDRESS.LIST TYPE="String">'
13 : Write File Line : '       <ADDRESS>'+ @@ERPNextAddress1 +'</ADDRESS>'
14 : Write File Line : '       <ADDRESS>'+ @@ERPNextAddress2 +'</ADDRESS>'
14a : Write File Line : '       <ADDRESS>'+ @@ERPNextAddress3 +'</ADDRESS>'
15 : Write File Line : '      </ADDRESS.LIST>'
16 : Write File Line : '      <MAILINGNAME.LIST TYPE="String">'
17 : Write File Line : '       <MAILINGNAME>'+$$Trim:$customer+'</MAILINGNAME>'
18 : Write File Line : '      </MAILINGNAME.LIST>'
;19 : Write File Line : '      <CREATEDDATE>'+$$GetTallyDate:$posting_date+ '</CREATEDDATE>'
;20 : Write File Line : '      <GUID>449cefda-ff79-434e-92d0-9946e7cda09e-00003449</GUID>'
;21 : Write File Line : '      <CURRENCYNAME>?</CURRENCYNAME>'
22 : Write File Line : '      <EMAIL>'+$contact_email+'</EMAIL>'
23 : Write File Line : '      <COUNTRYNAME>India</COUNTRYNAME>'
24 : Write File Line : '      <GSTREGISTRATIONTYPE>Regular</GSTREGISTRATIONTYPE>'
25 : Write File Line : '      <VATDEALERTYPE>Regular</VATDEALERTYPE>'
26 : Write File Line : '      <PARENT>Sundry Debtors</PARENT>'
27 : Write File Line : '      <CREATEDBY>laxman</CREATEDBY>'
28 : Write File Line : '      <COUNTRYOFRESIDENCE>India</COUNTRYOFRESIDENCE>'
29 : Write File Line : '      <LEDGERFAX>'++'</LEDGERFAX>'
30 : Write File Line : '      <LEDGERCONTACT>'+$contact_display+'</LEDGERCONTACT>'
31 : Write File Line : '      <LEDGERMOBILE>'+$contact_mobile+'</LEDGERMOBILE>'
32 : Write File Line : '      <PARTYGSTIN>'+@@ERPNextGSTIN+'</PARTYGSTIN>'
33 : Write File Line : '      <LEDSTATENAME>'+$$GetGSTStateName:($$StringPart:$place_of_supply:0:2) +'</LEDSTATENAME>'
34 : Write File Line : '      <ISBILLWISEON>Yes</ISBILLWISEON>'
35 : Write File Line : '      <ISCHEQUEPRINTINGENABLED>Yes</ISCHEQUEPRINTINGENABLED>'
36 : Write File Line : '      <LANGUAGENAME.LIST>'
37 : Write File Line : '       <NAME.LIST TYPE="String">'
38 : Write File Line : '        <NAME>'+$$Trim:$customer+'</NAME>'
39 : Write File Line : '       </NAME.LIST>'
40 : Write File Line : '       <LANGUAGEID> 1033</LANGUAGEID>'
41 : Write File Line : '      </LANGUAGENAME.LIST>'
42 : Write File Line : '     </LEDGER>'
42a : End If

43 : Write File Line : '     <VOUCHER REMOTEID="ERPSalesInvoice'+$name+'" VCHTYPE="'+@@EXPNextVouchertype+'" ACTION="Create" OBJVIEW="Invoice Voucher View">'
44 : Write File Line : '      <ADDRESS.LIST TYPE="String">'
45 : Write File Line : '       <ADDRESS>'+@@ERPNextAddress1+'</ADDRESS>'
46 : Write File Line : '       <ADDRESS>'+@@ERPNextAddress2+'</ADDRESS>'
46a : Write File Line : '       <ADDRESS>'+@@ERPNextAddress3+'</ADDRESS>'
47 : Write File Line : '      </ADDRESS.LIST>'
48 : Write File Line : '      <BASICBUYERADDRESS.LIST TYPE="String">'
49 : Write File Line : '       <BASICBUYERADDRESS>'+@@ERPNextAddress1+'</BASICBUYERADDRESS>'
50 : Write File Line : '       <BASICBUYERADDRESS>'+@@ERPNextAddress2+'</BASICBUYERADDRESS>'
51 : Write File Line : '       <BASICBUYERADDRESS>'+@@ERPNextAddress3+'</BASICBUYERADDRESS>'
52a : Write File Line : '      </BASICBUYERADDRESS.LIST>'
52 : Write File Line : '      <DATE>'+##vDate+'</DATE>'
53 : Write File Line : '      <STATENAME>'+$$GetGSTStateName:($$StringPart:$place_of_supply:0:2) +'</STATENAME>'
54 : Write File Line : '      <COUNTRYOFRESIDENCE>India</COUNTRYOFRESIDENCE>'
55 : Write File Line : '      <PARTYGSTIN>'+@@ERPNextGSTIN+'</PARTYGSTIN>'
56 : Write File Line : '      <PARTYNAME>'+$$Trim:$customer+'</PARTYNAME>'
57 : Write File Line : '      <PARTYLEDGERNAME>'+$$Trim:$customer+'</PARTYLEDGERNAME>'
58 : Write File Line : '      <PLACEOFSUPPLY>'+$$GetGSTStateName:($$StringPart:$place_of_supply:0:2) +'</PLACEOFSUPPLY>'
59 : Write File Line : '      <VOUCHERTYPENAME>'+@@EXPNextVouchertype+'</VOUCHERTYPENAME>'
60 : Write File Line : '      <VOUCHERNUMBER>'+$Name+'</VOUCHERNUMBER>'
61 : Write File Line : '      <BASICBASEPARTYNAME>'+$$Trim:$customer+'</BASICBASEPARTYNAME>'
62 : Write File Line : '      <FBTPAYMENTTYPE>Default</FBTPAYMENTTYPE>'
63 : Write File Line : '      <PERSISTEDVIEW>Invoice Voucher View</PERSISTEDVIEW>'
64 : Write File Line : '      <CONSIGNEEGSTIN>'+@@ERPNextGSTIN+'</CONSIGNEEGSTIN>'
65 : Write File Line : '      <BASICBUYERNAME>'+$$Trim:$customer+'</BASICBUYERNAME>'
;66 : Write File Line : '      <BASICDATETIMEOFINVOICE>13-Jul-2020 at 09:28</BASICDATETIMEOFINVOICE>'
;67 : Write File Line : '      <BASICDATETIMEOFREMOVAL>13-Jul-2020 at 09:28</BASICDATETIMEOFREMOVAL>'
68 : Write File Line : '      <CONSIGNEESTATENAME>'+$$GetGSTStateName:($$StringPart:$place_of_supply:0:2) +'</CONSIGNEESTATENAME>'
69 : Write File Line : '      <ENTEREDBY>erpnext</ENTEREDBY>'
70 : Write File Line : '      <EFFECTIVEDATE>'+##vDate+'</EFFECTIVEDATE>'
71 : Write File Line : '      <ISINVOICE>Yes</ISINVOICE>'
72 : Write File Line : '      <ISVATDUTYPAID>Yes</ISVATDUTYPAID>'
72a :Walk Collection: items
73 : Write File Line : '      <ALLINVENTORYENTRIES.LIST>'
74 : Write File Line : '        <BASICUSERDESCRIPTION.LIST TYPE="String">'
74a : Write File Line : '         <BASICUSERDESCRIPTION>'+$$Trim:$description+'</BASICUSERDESCRIPTION>'
74b : Write File Line : '        </BASICUSERDESCRIPTION.LIST>'
	   
74c : Write File Line : '       <STOCKITEMNAME>'+$item_code+'</STOCKITEMNAME>'
75 : Write File Line : '       <ISDEEMEDPOSITIVE>No</ISDEEMEDPOSITIVE>'
76 : Write File Line : '       <RATE>'+$$String:$rate+'/'+$baseunits:StockITem:$item_code+'</RATE>'
77 : Write File Line : '       <AMOUNT>'+$$String:$amount+'</AMOUNT>'
78 : Write File Line : '       <ACTUALQTY> '+$$String:$qty+' '+$baseunits:StockItem:$item_code+'</ACTUALQTY>'
79 : Write File Line : '       <BILLEDQTY> '+$$String:$qty+' '+$baseunits:StockItem:$item_code+'</BILLEDQTY>'
80 : Write File Line : '       <BATCHALLOCATIONS.LIST>'
81 : Write File Line : '        <GODOWNNAME>Main Location</GODOWNNAME>'
82 : Write File Line : '        <BATCHNAME>'+$$Trim:@@ERPNextBatchName+'</BATCHNAME>'
82a : Log: @@ERPNextBatchName
83 : Write File Line : '        <AMOUNT>'+$$String:$amount+'</AMOUNT>'
84 : Write File Line : '        <ACTUALQTY>'+$$String:$qty+' '+$baseunits:StockItem:$item_code+'</ACTUALQTY>'
85 : Write File Line : '        <BILLEDQTY>'+$$String:$qty+' '+$baseunits:StockItem:$item_code+'</BILLEDQTY>'
86 : Write File Line : '       </BATCHALLOCATIONS.LIST>'
87 : Write File Line : '       <ACCOUNTINGALLOCATIONS.LIST>'
88 : Write File Line : '        <LEDGERNAME>'+@@ERPNextSalesAccount+'</LEDGERNAME>'
89 : Write File Line : '        <ISDEEMEDPOSITIVE>No</ISDEEMEDPOSITIVE>'
90 : Write File Line : '        <AMOUNT>'+$$String:$amount+'</AMOUNT>'
91 : Write File Line : '       </ACCOUNTINGALLOCATIONS.LIST>'
92 : Write File Line : '      </ALLINVENTORYENTRIES.LIST>'
92a : End Walk
93 : Write File Line : '      <LEDGERENTRIES.LIST>'
94 : Write File Line : '       <LEDGERNAME>'+$$Trim:$customer+'</LEDGERNAME>'
95 : Write File Line : '       <ISDEEMEDPOSITIVE>Yes</ISDEEMEDPOSITIVE>'
96 : Write File Line : '       <ISPARTYLEDGER>Yes</ISPARTYLEDGER>'
97 : Write File Line : '       <ISLASTDEEMEDPOSITIVE>Yes</ISLASTDEEMEDPOSITIVE>'
98 : Write File Line : '       <AMOUNT>-'+$$String:$rounded_total+'</AMOUNT>'
99 : Write File Line : '       <BILLALLOCATIONS.LIST>'
100 : Write File Line : '        <NAME>'+$name+'</NAME>'
101 : Write File Line : '        <BILLTYPE>New Ref</BILLTYPE>'
102 : Write File Line : '        <AMOUNT>-'+$$String:$rounded_total+'</AMOUNT>'
103 : Write File Line : '       </BILLALLOCATIONS.LIST>'
104 : Write File Line : '      </LEDGERENTRIES.LIST>'

105 : Walk Collection: taxes
105a : Write File Line : '      <LEDGERENTRIES.LIST>'
106 : Write File Line : '       <LEDGERNAME>'+@@ERPNextGSTAccount+'</LEDGERNAME>'
107 : Write File Line : '       <METHODTYPE>GST</METHODTYPE>'
108 : Write File Line : '       <ISDEEMEDPOSITIVE>No</ISDEEMEDPOSITIVE>'
109 : Write File Line : '       <REMOVEZEROENTRIES>Yes</REMOVEZEROENTRIES>'
110 : Write File Line : '       <AMOUNT>'+$$String:$tax_amount+'</AMOUNT>'
111 : Write File Line : '       <VATEXPAMOUNT>'+$$String:$tax_amount+'</VATEXPAMOUNT>'
112 : Write File Line : '      </LEDGERENTRIES.LIST>'
113 : End Walk

121 : Write File Line : '      <LEDGERENTRIES.LIST>'
122 : Write File Line : '       <ROUNDTYPE>Normal Rounding</ROUNDTYPE>'
123 : Write File Line : '       <LEDGERNAME>ROUND OFF</LEDGERNAME>'
124 : Write File Line : '       <METHODTYPE>As Total Amount Rounding</METHODTYPE>'
125 : Write File Line : '       <ISDEEMEDPOSITIVE>No</ISDEEMEDPOSITIVE>'
126 : Write File Line : '       <REMOVEZEROENTRIES>Yes</REMOVEZEROENTRIES>'
127 : Write File Line : '       <ROUNDLIMIT> 1</ROUNDLIMIT>'
128 : Write File Line : '       <AMOUNT>'+$$String:$base_rounding_adjustment+'</AMOUNT>'
129 : Write File Line : '       <VATEXPAMOUNT>'+$$String:$base_rounding_adjustment+'</VATEXPAMOUNT>'
130 : Write File Line : '      </LEDGERENTRIES.LIST>'

132 : Write File Line : '      <UDF:RPBROKERNAME.LIST DESC="`RPBrokerName`" ISLIST="YES" TYPE="String" INDEX="32200">'
134 : Write File Line : '       <UDF:RPBROKERNAME DESC="`RPBrokerName`">'+@@ERPNextSalesman+'</UDF:RPBROKERNAME>'
136 : Write File Line : '      </UDF:RPBROKERNAME.LIST>'

138 : Write File Line : '     </VOUCHER>'



[System: Formula]
	
	ERPNextGSTAccount : If $account_head = 'SGST - ST' then "State Tax (SGST)" else +
						if $account_head = 'CGST - ST' then "Central Tax (CGST)" else +
						if $account_head = 'IGST - ST' then "Integrated Tax (IGST)" else ""

	ERPNextBatchName  : $$GetItemBatch:$item_code:$item_name:$description
						;If $item_code = "TE9SU" or $item_code = "TE9MU" then $serial_no else+
						;if $item_code in ("Tally Software Services Gold 1 Year", "TE9 Upgrade", "TSS Auditor", "TSS Silver") then $$GetTSSBatch:$item_code else  "Primary Batch"

	ERPNextAddress1 : $$Trim_Address:($$StringWordEx:$address_display:"<br>":1)
	ERPNextAddress2 : $$Trim_Address:($$StringWordEx:$address_display:"<br>":2)
	ERPNextAddress3 : $$Trim_Address:($$StringWordEx:$address_display:"<br>":3)

	ERPNextGSTIN : if $$IsEmpty:$customer_gstin then $billing_address_gstin else $customer_gstin

	ERPNextSalesman : $$GetERPNextSalesman:$Owner
	
	EXPNextVouchertype : if $name contains "ST" then "Sales - GST" else "Sales Tax Free"
	
	ERPNextSalesAccount : if $parent contains "ST" then "Sales - GST" else "Tax Free Sales"

